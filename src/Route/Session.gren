module Route.Session exposing
    ( create
    )


import Bytes exposing (Bytes)
import Crypto
import Db
import Email exposing (Email)
import HttpServer exposing (Request)
import HttpServer.Response as Response exposing (Response)
import Json.Decode
import Json.Encode
import Session exposing (Session)
import Task exposing (Task)
import User exposing (User)


type Error
    = InvalidEmail
    | DbError Db.Error


create : 
    { db : Db.Connection
    , secureContext : Crypto.SecureContext
    , request : Request
    , response : Response
    }
    -> Task Never Response
create { db, secureContext, request, response } =
    findOrCreateUser db request.body
        |> Task.andThen (createSession db secureContext )
        |> Task.map (createSuccess response)
        |> Task.onError (createFailed response)


findOrCreateUser : Db.Connection -> Bytes -> Task Error User
findOrCreateUser db requestBody =
    when getEmail requestBody is
        Nothing ->
            Task.fail InvalidEmail

        Just email ->
            User.findOrCreate db email
                |> Task.mapError DbError


createSession : Db.Connection -> Crypto.SecureContext -> User -> Task Error Session
createSession db secureContext user =
    Session.create { db = db, secureContext = secureContext, user = user }
        |> Task.mapError DbError


createSuccess : Response -> Session -> Response
createSuccess response session =
    let
        responseJson =
            Json.Encode.object
                [ { key = "fetchSessionToken"
                  , value = Json.Encode.string session.fetchSessionToken
                  }
                ]
    in
    response
        |> Response.setHeader "Content-Type" "application/json"
        |> Response.setBody (Json.Encode.encode 0 responseJson)


createFailed : Response -> Error -> Task x Response
createFailed response error =
    when error is
        InvalidEmail ->
            response
                |> Response.setStatus 400
                -- TODO: helpful error message in json body
                |> Task.succeed

        DbError e ->
            response
                |> Response.setStatus 500
                -- TODO: helpful error message in json body
                |> Task.succeed


getEmail : Bytes -> Maybe Email
getEmail bytes =
    bytes
        |> Bytes.toString
        |> Maybe.andThen decodeEmail
        |> Maybe.andThen Email.fromString


decodeEmail : String -> Maybe String
decodeEmail json =
    json
        |> Json.Decode.decodeString emailDecoder
        |> Result.toMaybe


emailDecoder : Json.Decode.Decoder String
emailDecoder =
    Json.Decode.field "email" Json.Decode.string
