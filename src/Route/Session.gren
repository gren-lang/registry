module Route.Session exposing
    ( create
    , confirmEmail
    , confirmEmailPath
    )


import Bytes exposing (Bytes)
import Crypto
import Db
import Email exposing (Email)
import HttpClient
import HttpServer exposing (Request)
import HttpServer.Response as Response exposing (Response)
import Postmark
import Json.Decode
import Json.Encode
import Route.Error
import Session exposing (Session)
import Task exposing (Task)
import Time
import Url.Builder
import User exposing (User)


type Error
    = DbError Db.Error
    | SendEmailFailed HttpClient.Error


-- PATHS


confirmEmailPath : { token : Maybe String } -> String
confirmEmailPath { token } =
    let
        query =
            when token is
                Just t -> [ Url.Builder.string "token" t ]
                Nothing -> []
    in
    Url.Builder.absolute [ "session", "confirm-email" ] query


-- ENDPOINTS


create : 
    { db : Db.Connection
    , postmark : Postmark.Configuration
    , secureContext : Crypto.SecureContext
    , requestData : { email : Email }
    , response : Response
    }
    -> Task Never Response
create { db, secureContext, postmark, requestData, response } =
    findOrCreateUser db requestData.email
        |> Task.andThen (createSession db secureContext )
        |> Task.andThen (sendEmailConfirmationLink postmark)
        |> Task.map (createSuccess response)
        |> Task.onError (createFailed response)


confirmEmail :
    { db : Db.Connection
    , requestData : { token : String }
    , response : Response
    }
    -> Task Never Response
confirmEmail { db, requestData, response } =
    let
        handleConfirmationCode session =
            Time.now
                |> Task.andThen (\now ->
                    if Session.emailConfirmationExpired now session then
                        Task.fail "Email confirmation link has expired"
                    else
                        when session.confirmationCode is
                            Just code ->
                                Task.succeed code

                            Nothing ->
                                Session.setConfirmationCode db session
                                    |> Task.mapError (\_ -> "Failed to set confirmation code")
                                    |> Task.andThen (\updatedSession ->
                                        when updatedSession.confirmationCode is
                                            Just code ->
                                                Task.succeed code

                                            Nothing ->
                                                Task.fail "Failed to generate confirmation code"
                                    )
                )

        handleDbError error =
            when error is
                Db.NoResultError ->
                    Task.fail "Invalid token"

                _ ->
                    Task.fail "Database error"
    in
    Session.getWithEmailConfirmationToken db requestData.token
        |> Task.onError handleDbError
        |> Task.andThen handleConfirmationCode
        |> Task.andThen (\code -> confirmEmailSuccess response code)
        |> Task.onError (Route.Error.invalidRequestData response)


confirmEmailSuccess : Response -> String -> Task x Response
confirmEmailSuccess response code =
    let
        json =
            Json.Encode.object
                [ { key = "confirmationCode", value = Json.Encode.string code }
                ]
    in
    response
        |> Response.setStatus 200
        |> Response.setHeader "Content-Type" "application/json"
        |> Response.setBody (Json.Encode.encode 0 json)
        |> Task.succeed


-- ACTIONS


findOrCreateUser : Db.Connection -> Email -> Task Error User
findOrCreateUser db email =
    User.findOrCreate db email
        |> Task.mapError DbError


createSession : Db.Connection -> Crypto.SecureContext -> User -> Task Error Session
createSession db secureContext user =
    Session.create { db = db, secureContext = secureContext, user = user }
        |> Task.mapError DbError


sendEmailConfirmationLink : Postmark.Configuration -> Session -> Task Error Session
sendEmailConfirmationLink postmarkConfig session =
    { to = session.user.email |> Email.toString
    , subject = "Gren: Confirm your email address"
    , textBody =
        "TODO: link with this token: " ++ session.emailConfirmationToken
    }
        |> Postmark.send postmarkConfig
        |> Task.mapError SendEmailFailed
        |> Task.map (\_ -> session)


-- RESPONSES


createSuccess : Response -> Session -> Response
createSuccess response session =
    let
        responseJson =
            Json.Encode.object
                [ { key = "fetchSessionToken"
                  , value = Json.Encode.string session.fetchSessionToken
                  }
                ]
    in
    response
        |> Response.setHeader "Content-Type" "application/json"
        |> Response.setBody (Json.Encode.encode 0 responseJson)


createFailed : Response -> Error -> Task x Response
createFailed response error =
    when error is
        DbError e ->
            -- TODO: log the actual error
            response
                |> Response.setStatus 500
                |> setErrorMessage "Database error"
                |> Task.succeed


        SendEmailFailed e ->
            -- TODO: log the actual error
            response
                |> Response.setStatus 500
                |> setErrorMessage "Failed to send email"
                |> Task.succeed


-- HELPERS


setErrorMessage : String -> Response -> Response
setErrorMessage message response =
    let
        body =
            Json.Encode.object
                [ { key = "message", value = Json.Encode.string message } ]
    in
    response
        |> Response.setHeader "Content-Type" "application/json"
        |> Response.setBody (Json.Encode.encode 0 body)
