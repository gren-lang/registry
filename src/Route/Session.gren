module Route.Session exposing
    ( create
    )


import Bytes exposing (Bytes)
import Crypto
import Db
import Email exposing (Email)
import HttpClient
import HttpServer exposing (Request)
import HttpServer.Response as Response exposing (Response)
import Postmark
import Json.Decode
import Json.Encode
import Session exposing (Session)
import Task exposing (Task)
import User exposing (User)


type Error
    = DbError Db.Error
    | SendEmailFailed HttpClient.Error


-- ENDPOINTS


create : 
    { db : Db.Connection
    , postmark : Postmark.Configuration
    , secureContext : Crypto.SecureContext
    , requestData : { email : Email }
    , response : Response
    }
    -> Task Never Response
create { db, secureContext, postmark, requestData, response } =
    findOrCreateUser db requestData.email
        |> Task.andThen (createSession db secureContext )
        |> Task.andThen (sendEmailConfirmationCode postmark)
        |> Task.map (createSuccess response)
        |> Task.onError (createFailed response)


-- ACTIONS


findOrCreateUser : Db.Connection -> Email -> Task Error User
findOrCreateUser db email =
    User.findOrCreate db email
        |> Task.mapError DbError


createSession : Db.Connection -> Crypto.SecureContext -> User -> Task Error Session
createSession db secureContext user =
    Session.create { db = db, secureContext = secureContext, user = user }
        |> Task.mapError DbError


sendEmailConfirmationCode : Postmark.Configuration -> Session -> Task Error Session
sendEmailConfirmationCode postmarkConfig session =
    { to = session.user.email |> Email.toString
    , subject = "Gren: Confirm your email address"
    , textBody =
        "Your confirmation code is: " ++ session.emailConfirmationCode
    }
        |> Postmark.send postmarkConfig
        |> Task.mapError SendEmailFailed
        |> Task.map (\_ -> session)


-- RESPONSES


createSuccess : Response -> Session -> Response
createSuccess response session =
    let
        responseJson =
            Json.Encode.object
                [ { key = "fetchSessionToken"
                  , value = Json.Encode.string session.fetchSessionToken
                  }
                ]
    in
    response
        |> Response.setHeader "Content-Type" "application/json"
        |> Response.setBody (Json.Encode.encode 0 responseJson)


createFailed : Response -> Error -> Task x Response
createFailed response error =
    when error is
        DbError e ->
            -- TODO: log the actual error
            response
                |> Response.setStatus 500
                |> setErrorMessage "Database error"
                |> Task.succeed


        SendEmailFailed e ->
            -- TODO: log the actual error
            response
                |> Response.setStatus 500
                |> setErrorMessage "Failed to send email"
                |> Task.succeed


-- HELPERS


setErrorMessage : String -> Response -> Response
setErrorMessage message response =
    let
        body =
            Json.Encode.object
                [ { key = "message", value = Json.Encode.string message } ]
    in
    response
        |> Response.setHeader "Content-Type" "application/json"
        |> Response.setBody (Json.Encode.encode 0 body)
