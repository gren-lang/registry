module Test.E2E.Helper exposing
    ( createSession
    , decodeJson
    , expectBadStatus
    , expectJson
    , expectNoResultError
    , initDb
    , get
    , post
    , postWithJson
    )


import Bytes exposing (Bytes)
import Crypto
import Db
import Email
import Expect exposing (Expectation)
import HttpClient exposing (Response)
import Json.Decode
import Json.Encode
import Result
import Session exposing (Session)
import Task exposing (Task)
import User


-- DB


initDb : HttpClient.Permission -> Db.Connection
initDb httpPerm =
    -- TODO: get db url from env
    Db.init httpPerm "http://localhost:12321/local"


expectNoResultError : Db.Error -> Expectation
expectNoResultError error =
    when error is
        Db.NoResultError ->
            Expect.pass

        e ->
            Expect.fail ("Expected NoResultError, got " ++ (Db.errorToString e))


-- HTTP


url : String -> String
url path =
    "http://localhost:3000" ++ path



get : HttpClient.Permission -> String -> Task HttpClient.Error (HttpClient.Response String)
get httpPerm path =
    url path
        |> HttpClient.get
        |> HttpClient.expectString
        |> HttpClient.send httpPerm


post : HttpClient.Permission -> String -> Task HttpClient.Error (HttpClient.Response {})
post httpPerm path =
    url path
        |> HttpClient.post
        |> HttpClient.expectAnything
        |> HttpClient.send httpPerm


postWithJson : HttpClient.Permission -> String -> Json.Encode.Value -> Task HttpClient.Error (HttpClient.Response Bytes)
postWithJson httpPerm path json =
    url path
        |> HttpClient.post
        |> HttpClient.expectBytes
        |> HttpClient.withJsonBody json
        |> HttpClient.send httpPerm


expectBadStatus : Int -> HttpClient.Error -> Expectation
expectBadStatus status error =
    when error is
        HttpClient.BadStatus r ->
            Expect.equal status r.statusCode
        _ ->
            Expect.fail <|
                "Expected HttpClient.BadStatus, Got: " ++
                (Debug.toString error)


decodeJson : Json.Decode.Decoder a -> Bytes -> Maybe a
decodeJson decoder bytes =
    Bytes.toString bytes
        |> Maybe.andThen (Json.Decode.decodeString decoder >> Result.toMaybe)


expectJson : Json.Decode.Decoder a -> a -> Bytes -> Expectation
expectJson decoder expected bytes =
    when Bytes.toString bytes is
        Nothing ->
            Expect.fail "Failed to decode bytes"

        Just json ->
            when Json.Decode.decodeString decoder json is
                Ok value ->
                    Expect.equal expected value

                Err error ->
                    Expect.fail (Json.Decode.errorToString error)


-- SESSIONS


{-| Create a session for a user with the example email.
-}
createSession : 
    { db : Db.Connection, secureContext : Crypto.SecureContext }
    -> Task Db.Error Session
createSession { db, secureContext } =
    User.findOrCreate db Email.example
        |> Task.andThen
            (\user ->
                Session.create 
                    { db = db
                    , secureContext = secureContext
                    , user = user 
                    }
            )
