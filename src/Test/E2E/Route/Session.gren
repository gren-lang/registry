module Test.E2E.Route.Session exposing (tests)


import Bytes
import Crypto
import Db
import Db.Encode
import Db.Decode
import Email
import Expect
import HttpClient
import Json.Decode
import Json.Encode
import Route.Session
import Session exposing (Session)
import String
import Task exposing (Task)
import Test.Runner.Effectful exposing (Test, await, awaitError, concat, describe, test)
import Test.E2E.Helper as Helper exposing (initDb, get, post, postWithJson, expectBadStatus, expectJson)
import Time
import User exposing (User)


tests : HttpClient.Permission -> Crypto.SecureContext -> Array Test
tests httpPerm secureContext =
    let
        db : Db.Connection
        db =
            initDb httpPerm
    in
    [ awaitError "GET /session" (get httpPerm "/session") <| \response ->
        test "404s" <| \_ ->
            expectBadStatus 404 response


    -- CREATE SESSION


    , describe "Create session" <|
        let
            goodEmail =
                Email.example |> Email.toString

            withNoEmail =
                post httpPerm "/session"

            withEmail email =
                postWithJson httpPerm "/session" <|
                    Json.Encode.object
                        [ { key = "email"
                          , value = Json.Encode.string email
                          }
                        ]
        in
        [ awaitError "POST /session with no email" withNoEmail <| \response ->
            test "Responds with 400 error" <| \_ ->
                expectBadStatus 400 response

        , awaitError "POST /session with bad email" (withEmail "asdf") <| \response ->
            test "Responds with 400 error" <| \_ ->
                expectBadStatus 400 response

        , await "POST /session with good email" (withEmail goodEmail) <| \response ->
            concat
                [ test "Responds successfully" <| \_ ->
                    Expect.equal 200 response.statusCode

                , await "Getting session info from db" 
                    (Db.getOne db
                        { query = 
                            """
                            select fetch_session_token from session
                            inner join user on (user.id = session.user_id)
                            where user.email = :email
                            order by session.created desc
                            limit 1
                            """
                        , parameters =
                            [ Db.Encode.string "email" goodEmail ]
                        , decoder =
                            Db.Decode.string "fetch_session_token"
                        }
                    )
                    (\expectedToken ->
                        test "responds with fetch session token" <| \_ ->
                            response.data
                                |> expectJson
                                    (Json.Decode.field "fetchSessionToken" Json.Decode.string)
                                    expectedToken
                    )
                ]


        -- CONFIRM EMAIL


        , describe "Confirm email" <|
            let
                createTestSession =
                    Helper.createSession
                        { db = db
                        , secureContext = secureContext
                        }

                httpGet token =
                    Route.Session.confirmEmailPath { token = token }
                        |> (\path ->
                            ("http://localhost:3000" ++ path)
                                |> HttpClient.get
                                |> HttpClient.expectBytes
                                |> HttpClient.send httpPerm
                        )
            in
            [ awaitError "GET with missing token" (httpGet Nothing) <| \responseError ->
                test "is client error" <| \_ ->
                    expectBadStatus 400 responseError

            , awaitError "GET with bad token" (httpGet (Just "bad token")) <| \responseError ->
                test "is client error" <| \_ ->
                    expectBadStatus 400 responseError

            , await "create test session" createTestSession <| \session ->
              await "GET with good token" (httpGet (Just session.emailConfirmationToken)) <| \response ->
                let
                    codeDecoder =
                        Json.Decode.field "confirmationCode" Json.Decode.string
                in
                concat
                    [ test "is success" <| \_ ->
                        Expect.equal 200 response.statusCode

                    , await "get updated session" (Session.getWithEmailConfirmationToken db session.emailConfirmationToken) <| \updatedSession ->
                        test "responds with confirmation code" <| \_ ->
                            when Helper.decodeJson codeDecoder response.data is
                                Just code ->
                                    Expect.all
                                        [ String.count >> Expect.equal 8
                                        , Just >> Expect.equal updatedSession.confirmationCode
                                        ]
                                        code

                                Nothing ->
                                    Expect.fail "Failed to decode confirmation code from response"

                    , await "GET again with same token (idempotent)" (httpGet (Just session.emailConfirmationToken)) <| \response2 ->
                        test "returns same code" <| \_ ->
                            Expect.equal response.data response2.data
                    ]

            , await "create session for expired email confirmation test" createTestSession <| \expiredSession ->
              await "update session to be expired (created 31 minutes ago)" (forceEmailConfirmationExpiration db expiredSession) <| \_ ->
              awaitError "GET with expired session token" (httpGet (Just expiredSession.emailConfirmationToken)) <| \responseError ->
                test "is client error" <| \_ ->
                    expectBadStatus 400 responseError
            ]
        ]
    ]


forceEmailConfirmationExpiration : Db.Connection -> Session -> Task Db.Error Int
forceEmailConfirmationExpiration db session =
    Task.await Time.now <| \now ->
        let
            past =
                now
                    |> Time.posixToMillis
                    |> (\m -> m - (31 * 60 * 1000)) -- 31 mins ago
                    |> Time.millisToPosix
        in
        Db.execute db
            { statement = "UPDATE session SET created = :created WHERE email_confirmation_token = :token"
            , parameters =
                [ Db.Encode.posix "created" past
                , Db.Encode.string "token" session.emailConfirmationToken
                ]
            }
